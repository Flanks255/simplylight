plugins {
    id 'java-library'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.gradle.userdev' version '7.0.157'
    id "me.modmuss50.mod-publish-plugin" version "0.4.5"
}

repositories {
    maven { url = 'https://maven.creeperhost.net/' }
    maven { url "https://www.cursemaven.com" }
}

def ENV = System.getenv()
version = "${mod_version}+${minecraft_version}-b${ENV.GITHUB_RUN_NUMBER ?: 9999}"
group = "com.flanks255"
base {
    archivesName = "simplylight"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

sourceSets.main.resources { srcDir 'src/generated/resources' }

runs {
    // applies to all the run configs below
    configureEach {
        //Limit ram usage for the dev environment to 4GB
        jvmArgument '-Xmx8G'

        modSource project.sourceSets.main
    }

    client {
        systemProperty 'neoforge.enabledGameTestNamespaces', "simplylight"
        programArguments.addAll('--width', '1280', '--height', '720')
        if  (ENV.MCDEVUSER) {
            programArguments.addAll('--username', ENV.MCDEVUSER)
        }
        if (ENV.MCDEVUUID) {
            programArguments.addAll('--uuid', ENV.MCDEVUUID)
        }
    }

    server {
        systemProperty 'neoforge.enabledGameTestNamespaces', "simplylight"
        programArgument '--nogui'
    }

    gameTestServer {
        systemProperty 'forge.enabledGameTestNamespaces', "simplylight"
    }

    data {
        programArguments.addAll '--mod', "simplylight", '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
    }
}


dependencies {
    implementation "net.neoforged:neoforge:${neo_version}"
    compileOnly "mezz.jei:jei-${jei_mc_version}-common-api:${jei_version}"//new
    compileOnly "mezz.jei:jei-${jei_mc_version}-neoforge-api:${jei_version}"
    runtimeOnly "mezz.jei:jei-${jei_mc_version}-neoforge:${jei_version}"

//    compileOnly fg.deobf("curse.maven:TOP-245211:4159743")
    runtimeOnly "curse.maven:JADE-324717:5493270"
    runtimeOnly "curse.maven:ATLAS-633577:5490697"
    runtimeOnly "curse.maven:CTM-267602:5587515"
    //compileOnly fg.deobf("curse.maven:BUILDINGGADGETS-298187:4423592")
}

jar {
    manifest {
        attributes([
                "Specification-Title": "simplylight",
                "Specification-Vendor": "Flanks255",
                "Specification-Version": "1",
                "Implementation-Title": project.name,
                "Implementation-Version": "${version}",
                "Implementation-Vendor" :"Flanks255",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            groupId project.group
            artifactId base.archivesName.get()
            version project.version
            from components.java
        }
    }

    repositories {
        if (ENV.SAPS_TOKEN) {
            maven {
                url "https://maven.saps.dev/minecraft"
                credentials {
                    username = "flanks"
                    password = "${ENV.SAPS_TOKEN}"
                }
            }
        }
    }
}
//https://modmuss50.github.io/mod-publish-plugin/
publishMods {
    dryRun = !providers.environmentVariable("CURSEFORGE_KEY").isPresent()
    file = jar.archiveFile
    changelog = file("./CHANGELOG.md").text
    modLoaders.add("NeoForge")
    type = BETA
    curseforge {
        accessToken = providers.environmentVariable("CURSEFORGE_KEY")
        projectId = project.curseforge_id
        minecraftVersions.add(project.minecraft_version)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}